buildscript {
    ext {
        log4jVersion = '2.10.0'
        jacksonVersion = '2.9.3'
    }
    repositories {
        mavenCentral()
    }
}

def getCurrentGitBranch() {
    def gitBranch = "Unknown"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}

def getLastGitCommit() {
    def lastGitCommit = "Unknown"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git log --pretty=format:"%h | %ad | %an" --date=iso -1'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            lastGitCommit = result.text.trim()
        }
    } catch (e) {
    }
    return lastGitCommit
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    
    sourceCompatibility = 1.8
    targetCompatibility = 1.8    

    repositories {
       mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    version = '9.1.1'
    
    compileJava.dependsOn(processResources)

    configurations {
        all*.exclude module: "logback-classic"
        all*.exclude module: "slf4j-log4j12"
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.name == 'log4j') {
                details.useTarget 'log4j:log4j:1.2.+'
            }
        }
    }
    
    eclipse {
        classpath {
             containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
             containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
        }
    }
    
    task wrapper(type: Wrapper) {
        gradleVersion = '3.4.1'
    }       

    def jarToCopy = copySpec {
        from "build/libs/${jar.archiveName}"
    }          
}

project(':acn-core') {
    dependencies {
    }
}

project(':acn-client') {
    dependencies {
        compile project(':acn-core')
    }
}